---
title: SkyBox
date: 2022-08-13
categories: [DirectX3D, DirectX3D]
tags: [directx3d]		# TAG는 반드시 소문자로 이루어져야함!
---

SkyBox
==============
* 게임안에서 배경역할
* SkyBox에는 두 종류의 타입이 있다.
  * Sphere(구)
  * Cube(사면체)


<br><br>

SkyBox Shader
==================

* ShaderDomain은 Shader마다 enum값 하나를 저장하여 이 값으로 Shader의 렌더링 순서를 정하도록 만듦<br> Forward는 불투명

* 래스터라이저(RS)는 CULL_FRONT
  * SkyBox안에 카메라가 있어서 카메라로 바라볼 때 안에서 바라보는 형태이므로 CULL_FRONT

* Depth Stencil(DS)는 LESS_EQUAL(깊이가 작거나 같으면 렌더링)
  * SkyBox는 항상 멀리 느껴지게 하면서 그려져야 하기 때문에 깊이 버퍼의 최대값인 1로 설정해야 하는데 Default값인 LESS로 한다면 깊이가 1이라 렌더링이 안되서 LESS_EQUAL


<br>


	pShader = new CGraphicsShader;
	pShader->CreateVertexShader(L"Shader\\std3d.fx", "VS_SkyBox");
	pShader->CreatePixelShader(L"Shader\\std3d.fx", "PS_SkyBox");

	pShader->SetShaderDomain(SHADER_DOMAIN::DOMAIN_FORWARD);
	pShader->SetRSType(RS_TYPE::CULL_FRONT);	
	pShader->SetDSType(DS_TYPE::LESS_EQUAL);
	pShader->SetBSType(BS_TYPE::DEFAULT);

    // 사용할 텍스쳐
	pShader->AddTexParamInfo(L"Output Texture", TEX_PARAM::TEX_0);      
	pShader->AddTexParamInfo(L"Cube Texture", TEX_PARAM::TEX_CUBE_0);   

	AddRes<CGraphicsShader>(L"SkyBoxShader", pShader, true);


<br><br>

SkyBox 세팅
====================

* 만들려고 하는 SkyBox는 배경역할을 해야 하기 때문에 설정이 필요하다

<br>

SkyBox를 바라보는 카메라가 SkyBox를 담당하는 Object로 다가간다고 해서 SkyBox와 가까워 지는게 느껴진다면 말이 되지 않는다.<br>
SkyBox는 카메라가 어디에 있든간에 배경역할을 수행해야 하기 때문이다.<br>
그래서 이 문제를 해결하기 위한 방법은 두가지가 있다.<br>
<br>
첫 번째로는 SkyBox Object를 Camera의 위치에 고정시켜놓는 방법이 있다.<br>
두 번째로는 Shader를 이용하는 방법이다.<br>
<br>
여기서 설명할 방법은 두번째이다.

<br><br>

Sphere SkyBox( 구 형태의 SkyBox)
======================================

일단 기본적으로 Sphere는 이 블로그에 있는 SphereMesh를 사용한다.<br>
이 Mesh는 반지름을 0.5로 설정했기 때문에 * 2를 하면 로컬 스페이스의 모든 정점의 반지름이 1이된다.<br>
로컬 스페이스에서 NDC를 사용하기 때문에 반지름이 1이면 최대 크기의 구가 된다.(-1 ~ 1이므로)<br>

<br>

셰이더로 처리하기 위해서 해결해야할 문제들이 있다.<br>
카메라가 바라보는 방향에 따라 바라보는 방향의 텍스처쪽 화면이 나와야 하도록 하기위해 카메라는 회전시켜야 하니<br> 
정점에서의 ViewSpace값의 회전행렬을 알아내 카메라에 곱한다.<br>

<br>
그리고 SkyBox가 항상 멀리 느껴지게 하기 위해 깊이를 1로 설정해야 한다.<br>
그렇게 만들기 위해 생각해야 할 부분이 있다.<br>
<br>

정점들을 World, View까지 보내놓고 나서 투영 행렬을 곱한다고 해도 우리가 원하는 투영좌표가 바로 나오지 않는다<br>
Projection Space는 행렬의 각 성분에 View Space의 z축의 값이 곱해진 형태로 나온다 <br>

        // 결과 x,y,z,w
        (Px * Vz , Py * Vz , Pz * Vz , Vz)

        (P는 Projection , V는 View,  x,y,z는 각각 축을 의미)

<br>

래스터라이저에서 이렇게 나온 값을 원근 나눗셈(w좌표로 모든 성분을 나눈다)을 하여 2D에서 사용할 값이 나오게 되는데<br>
래스터라이저로 보내기전에 깊이 값으로 사용할 z축(Pz * Vz)에 w좌표를 넣게되면<br>
래스터화 하면서 z축의 값이 1으로 고정된다.

        // 원근 나눗셈을 하기 전 z축을 w으로 변경
        (Px * Vz , Py * Vz , Vz , Vz)

        // 원근 나눗셈을 한 후
        (Px, Py, 1, 1)

이렇게 하고나서 Pixel Shader에서 Sphere Texture를 샘플링하면 된다.

<br>

Cube SkyBox(사면체 형태의 SkyBox)
===================================
Vertex Shader에서는 Sphere와 동일하다.<br>
다른 점은 Pixel Shader에서 샘플링할 때 차이가 있다.<br><br>

Cube Texture로 하려고 하기 위해서는 텍스쳐가 6장이 하나의 이미지로 저장된 리소스가 필요하다.<br>
DirectX에도 6개단위로 묶여있는 텍스쳐를 전달할 수 있는게 TextureCube 라는 타입으로 지원한다.<br>
(텍스쳐파일 안에 자기가 큐브타입이라는 플래그가 있어야함 , D3D11 Resource Misc Flag가 CubeTexture)<br><br>

이렇게 6개로 이루어진 TextureCube가 있다면 다음에 해야 할 일이 샘플링인데 기존의 샘플링과 달리<br>
UV가 float3타입으로, 정점의 위치가 곧 원점에서의 방향벡터이고 이 방향벡터로 UV값을 샘플링한다.<br>
(사면체에서의 정 가운데에서 방향벡터를 받아 각 알맞은 텍스쳐 면을 샘플링하는 느낌.)


<br>

SkyBox HLSL
=======================

<br>

    VTX_SKY_OUT VS_SkyBox(VTX_SKY_IN _in)
    {
        VTX_SKY_OUT output = (VTX_SKY_OUT) 0.f;
                
        // 반지름이 0.5 인 원을 반지름 1로 변경    
        float3 vViewPos = _in.vPos * 2.f; // 정점들의 위치가 1이므로 뷰행렬을 곱하지 않아도 된다.
        
        // View 의 회전만 적용시켜서, 카메라 앞에 원하는 부위를 배치시킴
        vViewPos = mul(float4(vViewPos, 0.f), g_matView);    
        
        // 투영
        float4 vProjPos = mul(float4(vViewPos, 1.f), g_matProj);
        vProjPos.z = vProjPos.w;    
        
        output.vPosition = vProjPos;      
        output.vUV = _in.vUV;
        output.vDirUV = normalize(_in.vPos);
        
        return output;
    }

    float4 PS_SkyBox(VTX_SKY_OUT _in) : SV_Target
    {    
        float4 vOutColor = (float4) float4(0.3f, 0.3f, 0.3f, 1.f);

        // Sphere Type
        if(0 == g_int_0)
        {
            if (g_btex_0)
            {
                vOutColor = g_tex_0.Sample(g_sam_0, _in.vUV);
            }
        }
        
        // Cube Type
        else if(1 == g_int_0)
        {
            if(g_btexcube_0)
            {
                vOutColor = g_texcube_0.Sample(g_sam_0, _in.vDirUV);    // UV가 방향벡터
            }        
        }
            
        return vOutColor;
    }
