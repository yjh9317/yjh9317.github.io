---
title: 그림자
date: 2022-09-07
categories: [DirectX3D, DirectX3D]
tags: [directx3d]		# TAG는 반드시 소문자로 이루어져야함!
---

그림자
====================
* 광원 시점에서의 렌더 타겟과 카메라 시점에서의 렌더 타겟의 깊이를 비교하여 그림자를 판정한다.
  
* 일반적으로 Directinal Light의 광원에 그림자를 적용시킨다.
  * Point Light는 Point Light의 6면(상하좌우앞뒤)를 체크해야 하기 때문에 연산이 매우 큼.
  * Spot Light는 Directional Light와 달리 직교 투영이 아닌 원근 투영으로 VolumeMesh를 사용해 적용


원리
======================
* 카메라 시점과 광원 시점에서의 깊이값을 비교하여 그림자를 판정한다
* 만약 깊이값이 다르다면 광원 시점에서 바라본 것과 카메라 시점에서 바라 본 것이 달라진다는 뜻이다.
  * 어떤 오브젝트 사이에 다른 오브젝트가 끼어있었다면 깊이값이 달라짐
<br><br>

과정
================

1. 먼저 광원 오브젝트 안에 있는 카메라를 이용하여 ShadowMap 렌더타겟에 픽셀값을 저장한다.
    * 깊이 텍스쳐를 사용하지 않은 이유는 깊이 텍스쳐는 ShaderResource나 RenderTarget과 공유가 안되기 때문에 다른 쉐이더에서 사용하지 못하기 때문에 렌더타겟에 저장해서 사용함.
    * 그래서 리소스로써 넘기기 위해서 깊이 텍스쳐가 아닌 렌더타겟을 사용해서 넘긴다.
    * 여기서 깊이 텍스쳐는 픽셀의 깊이값을 비교하여 깊이 판정만 한다. <br><br>

2. 그 후에 광원을 렌더링할 때 ShadowMap에 있는 렌더타겟과 광원 카메라의 View,Proj 행렬을 가져온다.

3. 메인 카메라의 Proj행렬 상태에서 역행렬을 곱해 World 행렬까지 보내고, 광원 카메라의 View ,Proj 행렬을 통해 광원 카메라로 보낸다.

4. ShadowMap과 카메라 시점에서의 깊이값을 비교하고 카메라 시점에서의 깊이가 더 멀다면, 그림자 판정

5. 그 후에 Merge Shader에서 그림자값을 받아서 그림자 진 부분은 빛을 약하게 비춘다.

<br><br>

ShadowMap MRT 생성
====================

* ShadowMap은 화면 해상도와 같을 필요가 없기 때문에 크기를 크게 잡는다.
* 렌더 타겟에 깊이값을 저장하기 위해 R32_FLOAT을 사용


<br>

	// =============
	// ShadowMap MRT
	// =============
	{
		Ptr<CTexture> arrTex[8] =
		{
			CResMgr::GetInst()->CreateTexture(L"ShadowMapTex" , 4096, 4096
				, DXGI_FORMAT_R32_FLOAT, D3D11_BIND_RENDER_TARGET | D3D11_BIND_SHADER_RESOURCE, true),
		};

		// Depth Stencil Texture 만들기
		Ptr<CTexture> pDepthStencilTex = CResMgr::GetInst()->CreateTexture(L"ShadowMapDSTex", 4096, 4096
			, DXGI_FORMAT_D32_FLOAT, D3D11_BIND_DEPTH_STENCIL, true);


		m_arrMRT[(UINT)MRT_TYPE::SHADOWMAP] = new CMRT;
		m_arrMRT[(UINT)MRT_TYPE::SHADOWMAP]->Create(1, arrTex, pDepthStencilTex);
	}


<br><br>

ShadowMap Shader
==================

        // ===============
        // ShdowMap Shader
        // MRT : ShadowMap MRT
        // RS : CULL_BACK
        // BS : Default
        // DS : Less
        // ===============
        struct VS_SHADOW_IN
        {
            float3 vPos : POSITION; 
        };

        struct VS_SHADOW_OUT
        {
            float4 vPosition : SV_Position;
            float4 vProjPos : POSITION;
        };

        VS_SHADOW_OUT VS_ShadowMap(VS_SHADOW_IN _in)
        {
            VS_SHADOW_OUT output = (VS_SHADOW_OUT) 0.f;

            output.vPosition = mul(float4(_in.vPos, 1.f), g_matWVP);
            output.vProjPos = output.vPosition;    
            
            return output;
        }

        float PS_ShadowMap(VS_SHADOW_OUT _in) : SV_Target
        {
            float fOut = 0.f;    
            fOut = _in.vProjPos.z / _in.vProjPos.w;
            return fOut;
        }



<br><br>

Directioanl Light
===================

        // ========================
        // Directional Light Shader
        // MRT      : Light
        // mesh     : RectMesh
        // RS       : CULL_BACK
        // DS       : NO_TEST_NO_WRITE
        // BS       : ONE_ONE

        #define LightIdx        g_int_0
        #define PositionTarget  g_tex_0
        #define NormalTarget    g_tex_1
        #define ShadowMap       g_tex_2

        #define LightVP         g_mat_0
        // ========================

        struct VS_DIR_IN
        {
            float3 vPos : POSITION;
        };

        struct VS_DIR_OUT
        {
            float4 vPosition : SV_Position;
        };

        VS_DIR_OUT VS_Directional(VS_DIR_IN _in)
        {
            VS_DIR_OUT output = (VS_DIR_OUT) 0.f;

            output.vPosition = float4(_in.vPos * 2.f, 1.f);
            
            return output;
        }

        struct PS_DIR_OUT
        {
            float4 vDiffuse : SV_Target0;
            float4 vSpecular : SV_Target1;
            float4 vShadowPow : SV_Target2;
        };

        PS_DIR_OUT PS_Directional(VS_DIR_OUT _in)
        {
            PS_DIR_OUT output = (PS_DIR_OUT) 0.f;
            
            float2 vUV = _in.vPosition.xy / vResolution.xy;
            
            float3 vViewPos = PositionTarget.Sample(g_sam_0, vUV).xyz;
            float3 vViewNormal = NormalTarget.Sample(g_sam_0, vUV).xyz;
            float fShadowPow = 0.f;
            
            // 물체가 그려지지 않은 영역은 빛을 받을 수 없다.
            if (vViewPos.z == 0.f)
                clip(-1);
                
            tLightColor color = (tLightColor) 0.f;
            CalculateLight3D(vViewPos, vViewNormal, LightIdx, color);    
                
            // 그림자 판정
            // ViewPos -> WorldPos
            float3 vWorldPos = mul(float4(vViewPos, 1.f), g_matViewInv).xyz;
            
            // WorldPos -> Light 투영
            float4 vLightProj = mul(float4(vWorldPos, 1.f), LightVP);
            
            // w 로 나눠서 실제 xy 투영좌표를 구함
            vLightProj.xyz /= vLightProj.w;    
            
            // 샘플링을 하기 위해서 투영좌표계를 UV 좌표계로 변환
            float2 vShadowMapUV = float2((vLightProj.x / 2.f) + 0.5f, -(vLightProj.y / 2.f) + 0.5f);  
            float fShadowMapDepth = ShadowMap.Sample(g_sam_0, vShadowMapUV).r;   
            
            // 광원에 기록된 깊이보다, 물체의 깊이가 더 멀 때, 그림자 판정
            if (vLightProj.z >= fShadowMapDepth + 0.00001f)
            {        
                fShadowPow = 0.9f;
            }
            
            output.vDiffuse     = (color.vDiff + color.vAmb);
            output.vSpecular    = color.vSpec;
            output.vShadowPow   = fShadowPow;
            
            output.vDiffuse.a   = 1.f;
            output.vSpecular.a  = 1.f;
            output.vShadowPow.a = 1.f;
                
            return output;
        }



<br><br>


Merge Shader
================


        // ==============================
        // Merge Shader
        // Mesh : RectMesh
        #define ColorTarget g_tex_0
        #define DiffuseTarget g_tex_1
        #define SpecularTarget g_tex_2
        #define ShadowPowTarget g_tex_3
        // ==============================
        struct VS_MERGE_IN
        {
            float3 vPos : POSITION;
        };

        struct VS_MERGE_OUT
        {
            float4 vPosition : SV_Position;
        };

        VS_MERGE_OUT VS_Merge(VS_MERGE_IN _in)
        {
            VS_MERGE_OUT output = (VS_MERGE_OUT) 0.f;

            output.vPosition = float4(_in.vPos * 2.f, 1.f);
            
            return output;
        }

        float4 PS_Merge(VS_MERGE_OUT _in) : SV_Target0
        {
            float4 vOutColor = (float4) 0.f;
            
            float2 vUV = _in.vPosition.xy / vResolution.xy;
            
            float3 vColor = ColorTarget.Sample(g_sam_0, vUV).xyz;
            float3 vDiffuse = DiffuseTarget.Sample(g_sam_0, vUV).xyz;
            float3 vSpecular = SpecularTarget.Sample(g_sam_0, vUV).xyz;
            float  fShadowPow = ShadowPowTarget.Sample(g_sam_0, vUV).x;
                
            vOutColor = float4(vColor * vDiffuse * (1.f - fShadowPow) 
                            + vSpecular * (1.f - fShadowPow), 1.f);
            
            
            return vOutColor;
        }