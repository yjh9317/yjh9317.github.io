---
title: Tessellation Stage
date: 2022-06-27
categories: [DirectX, DirectX]
tags: [directx]		# TAG는 반드시 소문자로 이루어져야함!
---

# tessellation

* tessellate이란 `평면이나 공간을 도형으로 완벽하게 덮는 것`을 말한다.

# 테셀레이션 단계(Tessellation Stage)

* 도형(Geometry)을 더 작은 삼각형으로 세분한 다음 새로 생성된 정점을 어떤 방식으로든 Offset하는 단계

* 즉, Mesh에 Detail을 더하기 위해 삼각형의 정점을 늘리는 단계이다.

* 그러면, 처음부터 High-poly mesh로 만들지 않느냐에 대한 이유는 다음과 같다.

  * 카메라의 거리같은 다른 요소들에 기반해서 동적으로 Mesh의 Detail을 조절할 수 있다.
  * 물리와 애니메이션은 Low-poly Mesh로 계산한 다음, High-poly Mesh로 만들면 더 낮은 빈도로 수행할 수 있어 성능을 높일 수 있다.
  * Low-poly Mesh를 메모리(disk,VRAM,RAM)에 저장한 다음, GPU가 High-poly으로 테셀레이션이 가능하다

<br>

# TESSELLATION PRIMITIVE TYPES

* 테셀레이션을 렌더링할 때, IA 단계에서 삼각형을 가져올 수 없다.

* 대신 `여러 control points(제어점)를 포함한 patches`를 사용한다

* patches는 1 ~ 32 개의 control points를 지원한다.

```
D3D11_PRIMITIVE_1_CONTROL_POINT_PATCH = 8,
D3D11_PRIMITIVE_2_CONTROL_POINT_PATCH = 9,
D3D11_PRIMITIVE_3_CONTROL_POINT_PATCH = 10,
D3D11_PRIMITIVE_4_CONTROL_POINT_PATCH = 11,
.
.
.
D3D11_PRIMITIVE_31_CONTROL_POINT_PATCH = 38,
D3D11_PRIMITIVE_32_CONTROL_POINT_PATCH = 39
```

* 하나의 삼각형에는 3개의 control points가 있다.

* 그래서 Mesh에 있는 삼각형으로 control points를 제공할 수도 있다.

<br>

## Vertex Shader와 Tessellation

* Vertex Shader에서 받은 patch control points를 통해 렌더링 파이프라인에 사용되기 때문에 사실상 Vertext Shader는 `control points에 대한 Vertex shader`가 된다

* 그래서 테셀레이션에 필요한 작업들은 Vertex Shader에서 수행할 수 있다.

* 위에서 말했듯이 애니메이션, 물리 계산은 tessellation하기 전에 vertex shader에서 더 낮은 빈도로 수행된다.


<br>

# Hull Shader

* Hull Shader는 테셀레이션 단계의 일부로, 다음과 같은 두 단계로 구성되어 있다.

## Constant Hull Shader(패치 상수 함수)

* 각 patch나 Mesh의 테셀레이션 요소들에 대한 작업을 수행하기 위한 단계

* 각 패치 당 수행되는 함수

* 테셀레이션 요소들은 얼마나 Tessellation을 할 지 정해준다.

```c++
// 사각형을 예시로 들어서 EdgeTess는 4의 크기, InsideTess는 2의 크기를 가짐
struct PatchTess
{
    float EdgeTess[4] : SV_TessFactor;
    float InsideTess[2] : SV_InsideTessFactor;
};
PatchTess ConstantHS(InputPatch<VertexOut, 4> patch,
uint patchID : SV_PrimitiveID)
{
    PatchTess pt;

    // 균등하게 patch를 3개로 Tessellate 수행
    pt.EdgeTess[0] = 3; // Left edge
    pt.EdgeTess[1] = 3; // Top edge
    pt.EdgeTess[2] = 3; // Right edge
    pt.EdgeTess[3] = 3; // Bottom edge
    pt.InsideTess[0] = 3; // u-axis (columns)
    pt.InsideTess[1] = 3; // v-axis (rows)
    return pt;
}
```

* Constant Hull Shader는 `InputPatch`로 정의되는 patch의 control points들을 사용한다

* Hull Shader가 호출되기 전에 Vertex Shader가 호출되는 것이므로, Vertex Shader를 통해 InputPatch가 결정되며 `시스템에서 각 패치의 ID를 제공해주는 SV_PrimitiveID 시멘틱`으로 patch들을 식별할 수 있다

* Constant Hull Shader는 Tessellation 인자들을 출력해야 하며 patch의 도형에 따라 달라진다.

<br>

* `SV_TessFactor`는 Patch의 각 변을 몇등분할 지에 대한 값
* `SV_InsideTessFactor`는 내부에 꼭짓점을 몇 개 만들지에 대한 값

## Control Point Hull Shader

* 정점마다 수행되는 함수

* 한 정점에서 수행되면 그 정점이 사용된 패치(삼각형)의 정보가 같이 들어온다
  
* 대괄호를 이용하여 쉐이더에서 필요한 정보들을 세팅

```c++
struct HullOut
{
  float3 PosL : POSITION;
};

[domain(“quad”)] // patch의 type으로  `tri, quad, isoline`와 같이 있다
[partitioning(“integer”)] // 사용할 타입(int,float 등등)
[outputtopology(“triangle_cw”)] // 시계,반시계 방향을 정함
[outputcontrolpoints(4)] // 사용하는 제어점 개수
[patchconstantfunc(“ConstantHS”)] // 사용할 패치 상수 함수
[maxtessfactor(64.0f)] // 한 면에서 최대 몇개까지 분할할 지
HullOut HS(InputPatch<VertexOut, 4> p, // 패치 구조체
uint i : SV_OutputControlPointID,      // 패치 안에서의 정점 ID 
uint patchId : SV_PrimitiveID)         // 패치의 ID
{
  HullOut hout;
  hout.PosL = p[i].PosL;
  return hout;
}
```
 
<br>

# Domain Shader

* HullShader 에서 전달한 정보를 토대로 정점을 생성 시키는 단계

* 생성된 정점에 대해서 Domain Shader 를 호출 시킨다.

* 주의할 점은 테셀레이션이 보간까진 해주진 않기 때문에 DomainShader 이후에 RasterRizer에 넘겨주기 전에 보간을 해서 넘겨줘야 한다.

```c++
struct DomainOut
{
  float4 PosH : SV_POSITION;
};

[domain(“quad”)]
DomainOut DS(PatchTess patchTess,
float2 uv : SV_DomainLocation,
const OutputPatch<HullOut, 4> quad)
{
  DomainOut dout;

  float3 v1 = lerp(quad[0].PosL, quad[1].PosL, uv.x);
  float3 v2 = lerp(quad[2].PosL, quad[3].PosL, uv.x);
  float3 p = lerp(v1, v2, uv.y);
  dout.PosH = mul(float4(p, 1.0f), gWorldViewProj);
  return dout;
}
```